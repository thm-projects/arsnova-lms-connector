plugins {
	id 'java-library'
	id "org.openrepose.gradle.plugins.jaxb" version "2.5.0"
}

jar {
	manifest {
		attributes 'Implementation-Title': 'Connector-Model', 'Implementation-Version': archiveVersion
	}
}

dependencies {
	api 'jakarta.xml.bind:jakarta.xml.bind-api'

	jaxb 'org.glassfish.jaxb:jaxb-xjc:2.3.3'
	jaxb 'org.glassfish.jaxb:jaxb-runtime:2.3.3'
	jaxb 'jakarta.xml.bind:jakarta.xml.bind-api:2.3.3'
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			versionMapping {
				usage('java-api') {
					fromResolutionOf('runtimeClasspath')
				}
				usage('java-runtime') {
					fromResolutionResult()
				}
			}
		}
	}
}

sourceSets {
	main {
		java {
			srcDir 'build/generated-sources/xjc'
		}
	}
}

jaxb {
	xsdDir = "${project.projectDir}/src/main/resources"
}

tasks.compileJava.dependsOn('xjc')
