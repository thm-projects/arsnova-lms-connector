allprojects {
	group = 'net.particify.arsnova.integrations'
	version = '1.0.2-SNAPSHOT'

	repositories {
		jcenter()
		mavenCentral()
		mavenLocal()
	}
}

subprojects {
	apply plugin: 'jacoco'
	apply plugin: 'java'
	apply plugin: 'maven-publish'

	sourceCompatibility = 1.8

	ext {
		springBootVersion = '2.4.1'
	}

	dependencies {
		implementation platform(group: 'org.springframework.boot', name: 'spring-boot-dependencies', version: springBootVersion)
		implementation group: 'org.springframework', name: 'spring-context'
		implementation group: 'org.springframework.security', name: 'spring-security-config'
		implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind'

		testImplementation group: 'junit', name: 'junit'
		testImplementation group: 'org.springframework', name: 'spring-test'
	}

	test { systemProperties 'property': 'value' }

	configurations { deployerJars }

	publishing {
		repositories {
			if (project.hasProperty('gitlabProjectId') || project.hasProperty('mavenRepoUrl')) {
				maven {
					if (project.hasProperty('gitlabProjectId')) {
						def gitlabHostUrl = project.hasProperty('gitlabHostUrl') ? gitlabHostUrl : 'gitlab.com'
						if (!(gitlabHostUrl ==~ /https?:.*/)) {
							gitlabHostUrl = 'https://' + gitlabHostUrl
						}
						url = "${gitlabHostUrl}/api/v4/projects/${gitlabProjectId}/packages/maven"
						if (project.hasProperty('gitlabAuthToken')) {
							credentials(HttpHeaderCredentials) {
								name = 'Job-Token'
								value = gitlabAuthToken
							}
							authentication {
								header(HttpHeaderAuthentication)
							}
						}
					} else {
						url = mavenRepoUrl
					}
				}
			}
		}
	}

	jacocoTestReport {
		reports {
			csv.enabled true
		}
	}
}
